buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.eclipse.jgit:org.eclipse.jgit:4.3.0.201604071810-r'
	}
}

import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.eclipse.jgit.api.DescribeCommand

def repositoryBuilder = new FileRepositoryBuilder()
repositoryBuilder.mustExist = true
repositoryBuilder.workTree = file('.')

def repository = repositoryBuilder.build()

def command = new DescribeCommand(repository)
def describe = command.call()

repository.close()

if(describe && describe.startsWith('v')) {
	version = describe.substring(1)
}

apply plugin: 'scala'
apply plugin: 'publishing'
apply plugin: 'maven-publish'

publishing {
	if(version == 'unspecified' || version ==~ /.*?-g.{7}/) {
		repositories.maven {
			name 'idgis-snapshots'
			url 'http://nexus.idgis.eu/content/repositories/snapshots/'
			credentials {
				username nexusUser 
				password nexusPassword
			}
		}
	} else {		
		repositories.maven {
			name 'idgis-releases'
			url 'http://nexus.idgis.eu/content/repositories/releases/'
			credentials {
				username nexusUser 
				password nexusPassword
			}
		}
	}
	
	publications {
		mavenJava(MavenPublication) {
			groupId 'nl.idgis.dav'
			artifact jar
			
			if(project.version == 'unspecified') {
				version = '0.0.1-SNAPSHOT'
			} else if(project.version ==~ /.*?-g.{7}/) {
				version = "${project.version}-SNAPSHOT"
			}
		}
	}
}

repositories {
	mavenCentral()
}

sourceSets {
	main {
		scala {
			srcDirs += 'build/twirl'
		}
	}
}

def javaVersion = '1.8'

def scalaVersion = '2.11.8'
def scalaAbiVersion = '2.11'

def playVersion = '2.4.3'

dependencies {
	compile "org.scala-lang:scala-library:${scalaVersion}"
	compile "com.typesafe.play:play-java_${scalaAbiVersion}:${playVersion}"
}

task compilePlayTwirlTemplates(type: TwirlCompile) {
	
	setPlatform([
		getJavaPlatform: {javaVersion},
		getPlayVersion: {playVersion},
		getScalaPlatform: {[
			getScalaCompatibilityVersion: {scalaAbiVersion},
			getScalaVersion: {scalaVersion}
		] as ScalaPlatform}
	] as PlayPlatform)
	
	defaultImports = 'JAVA'
	
	source = files('src/main/twirl')
	include '**/*.scala.xml'
	
	outputDirectory = file('build/twirl')
}

compileScala.dependsOn(compilePlayTwirlTemplates)