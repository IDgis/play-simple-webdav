buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.eclipse.jgit:org.eclipse.jgit:4.3.0.201604071810-r'
	}
}

import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.eclipse.jgit.api.DescribeCommand

def repositoryBuilder = new FileRepositoryBuilder()
repositoryBuilder.mustExist = true
repositoryBuilder.workTree = file('.')

def repository = repositoryBuilder.build()

def command = new DescribeCommand(repository)
def describe = command.call()

repository.close()

if(describe && describe.startsWith('v')) {
	version = describe.substring(1)
}

apply plugin: 'scala'
apply plugin: 'publishing'
apply plugin: 'maven-publish'

def javaVersion = '1.8'

def scalaVersion = '2.11.8'
def scalaAbiVersion = '2.11'

def playVersion = '2.4.3'

version '0.0.1-SNAPSHOT'
group 'nl.idgis.dav'

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/IDgis/play-simple-webdav"
			credentials {
				username = System.getenv("GITHUB_USER") ?: ghUser
				password = System.getenv("GITHUB_TOKEN") ?: ghKey
			}
		}
	}
	
	publications {
		gpr(MavenPublication) {
			from(components.java)
		}
	}
}

repositories {
	mavenCentral()
}

sourceSets {
	main {
		scala {
			srcDirs += 'build/twirl'
		}
	}
}

dependencies {
	compile "org.scala-lang:scala-library:${scalaVersion}"
	compile "com.typesafe.play:play-java_${scalaAbiVersion}:${playVersion}"
}

jar {
	manifest {
		attributes("Implementation-Title": project.name)
		if(project.version != 'unspecified') {
			attributes("Implementation-Version": project.version)
		}
	}
}

task compilePlayTwirlTemplates(type: TwirlCompile) {
	
	setPlatform([
		getJavaPlatform: {javaVersion},
		getPlayVersion: {playVersion},
		getScalaPlatform: {[
			getScalaCompatibilityVersion: {scalaAbiVersion},
			getScalaVersion: {scalaVersion}
		] as ScalaPlatform}
	] as PlayPlatform)
	
	defaultImports = 'JAVA'
	
	source = files('src/main/twirl')
	include '**/*.scala.xml'
	
	outputDirectory = file('build/twirl')
}

compileScala.dependsOn(compilePlayTwirlTemplates)
